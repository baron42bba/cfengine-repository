#######################################################
# Prepare client sub directories in repository
#######################################################
#
# Scenario:
# Prepare content of sub directories for clients to copy 
# them to autorun locations - clients copy policy files 
# to be executed from sub directories in repository with
# names corresponding to their IP address/hostname/tags.
# Policy files in those sub directories are hard links to
# actuall ones found in multiple locations on the hub 
#
# Original author: Jurica Borozan
# Version: 1.2.0
#


##
## When testing uncoment
##
#body common control
#{
#  any::
#    bundlesequence => { "jb_setup_client_dirs" };
#    inputs => { "$(sys.libdir)/stdlib.cf" };
#}

##
##
##
bundle agent jb_setup_client_dirs
{
  meta:
    "tags" slist => { "autorun" };

  vars:

    linux.am_policy_hub::

#  set input files for all bundles
      "params_files" slist => findfiles("$(this.promise_dirname)/$(this.bundle).json", "$(this.promise_dirname)/$(this.bundle).yaml");
      "params"        data => readdata(nth("params_files", 0), "auto");

# rm command
      "cmd_dirs" slist => { "/usr/bin", "/bin" };
      "cmds" slist => { "rm" };

      "cmd[$(cmds)]" string => "$(cmd_dirs)/$(cmds)",
        ifvarclass => fileexists("$(cmd_dirs)/$(cmds)");

# repository dir, fetch it from augments or data file
      "repository_dir" string => "$(def.jb_repository_dir)/cfengine",
        ifvarclass => isvariable("def.jb_repository_dir");

      "repository_dir" string => "$(params[repository_dir])",
        ifvarclass => not( isvariable("def.jb_repository_dir") );

# calculated parameters: directories
      "subdirs"                  slist => getindices("params[tags]"); 

      "existing_files_subdirs"         slist => lsdir("$(repository_dir)", ".*", "false");

      "files_subdirs_to_delete"        slist => difference("existing_files_subdirs", "subdirs");


  classes:

    linux.am_policy_hub::

# flag if directories exist
      "no_$(subdirs)" not => isdir("$(repository_dir)/$(subdirs)");

      "have_dirs_to_delete" expression => some(".*", "files_subdirs_to_delete");


  files:

    linux.am_policy_hub::

# create directories that do not exist

     "$(repository_dir)/$(subdirs)/."
         comment => "Create tag sub directory: $(subdirs)",
          create => "true",
      ifvarclass => canonify("no_$(subdirs)");


  commands:

    linux.am_policy_hub::

# remove unwanted files, directories and their contents
      "$(cmd[rm]) -fr $(repository_dir)/$(files_subdirs_to_delete)"
           comment => "Delete file or sub directory $(files_subdirs_to_delete) if should not exist ir repository",
        ifvarclass => "have_dirs_to_delete";


  methods:

    linux.am_policy_hub::

# per each subdirectory
      "run" usebundle => jb_setup_client_files("$(repository_dir)", "$(subdirs)", @(params));

}

##
##
##
bundle agent jb_setup_client_files(repository_dir, subdir, params)
{
  vars:

# all cf files, default and tag ones, must create intermediate lists and then join them
     "default_files" slist => getvalues("params[default]");
     "subdir_files"  slist => getvalues("params[tags][$(subdir)]");

     "all_files"     slist => { @(default_files), @(subdir_files) };
     "files"         slist => unique("all_files");

  classes:

    "is_specific_file_$(files)_in_$(params[policies_dirs])" expression => fileexists("$(params[policies_dirs])/$(subdir)_$(files)");

    "is_file_$(files)_in_$(params[policies_dirs])" expression => fileexists("$(params[policies_dirs])/$(files)"),
      ifvarclass => not( canonify("is_specific_file_$(files)_in_$(params[policies_dirs])") );


  files:

# first delete eventualy existing links or files
    "$(repository_dir)/$(subdir)/.*\.(cf|json|yaml)"
           comment => "Delete all other files or links in tag sub directory",
            delete => tidy,
       file_select => ex_list(@(files));

# create hard link from this subdirectory to a file in repository directory

# files that have same content for all
    "$(repository_dir)/$(subdir)/$(files)"
                comment => "Creating link to $(files)",
      move_obstructions => "true",
              link_from => jb_ln_h("$(params[policies_dirs])/$(files)"),
             ifvarclass => canonify("is_file_$(files)_in_$(params[policies_dirs])");

# or site specific content (prefixed for with site identification)
    "$(repository_dir)/$(subdir)/$(files)"
                comment => "Creating link to $(files)",
      move_obstructions => "true",
              link_from => jb_ln_h("$(params[policies_dirs])/$(subdir)_$(files)"),
             ifvarclass => canonify("is_specific_file_$(files)_in_$(params[policies_dirs])");

}

##
##
##
body link_from jb_ln_h(x)
{
  link_type => "hardlink";
  source => "$(x)";
}


